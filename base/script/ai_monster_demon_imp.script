/***********************************************************************

ai_monster_demon_imp.script

monster_demon_imp
monster_demon_imp_crawler

***********************************************************************/

#define IMP_RUNDISTANCE		192
#define IMP_WALKTURN		65
#define IMP_ATTACK_RATE		1
#define IMP_LEAP_RATE		7
#define IMP_LEAP_SPEED		650
#define IMP_LEAP_MAXHEIGHT	48
#define IMP_DODGE_RATE		8
#define IMP_LEAP_RANGE_MIN	200
#define IMP_LEAP_RANGE_MAX	480
#define IMP_NOFOVTIME		4

object monster_demon_imp : monster_base {
	float		nextDodge;
	float		nextAttack;
	float		nextLeap;
	float		nextNoFOVAttack;
	vector		jumpVelocity;
	entity		combat_node;

	string		range_attack_anim;
	
	// States
	void		state_Begin();
	void		state_Idle();

	// attacks
	float		check_attacks();
	void		do_attack( float attack_flags );
	void		combat_range();
	void		combat_leap();
	void		combat_melee();
	void		combat_dodge_left();
	void		combat_dodge_right();
	void		path_jump();

	void		init();
	
	// torso anim states
	void		Torso_Idle();
	void		Torso_Pain();
	void		Torso_MeleeAttack();
	void		Torso_RangeAttack();
	void		Torso_TurretAttack();
	void		Torso_TurretAttackIdle();
	void		Torso_LeapAttack();
	
	// legs anim states
	void		Legs_Idle();
	void		Legs_Walk();
	void		Legs_Run();
	void		Legs_DodgeLeft();
	void		Legs_DodgeRight();
};

/***********************************************************************

	Torso animation control

***********************************************************************/

void monster_demon_imp::Torso_Idle() {
	idleAnim( ANIMCHANNEL_TORSO, "stand" );
	
	eachFrame {
		if ( AI_PAIN ) { animState( ANIMCHANNEL_TORSO, "Torso_Pain", 3 ); }
	}
}

void monster_demon_imp::Torso_Pain() {
	string animname;
	float nextpain;
	float currenttime;
	
	animname = getPainAnim();
	playAnim( ANIMCHANNEL_TORSO, animname );

	nextpain = sys.getTime() + 0.25;
	
	while( !animDone( ANIMCHANNEL_TORSO, 6 ) ) {
		if ( AI_PAIN ) {
			currenttime = sys.getTime();
			if ( currenttime > nextpain ) {
				animState( ANIMCHANNEL_TORSO, "Torso_Pain", 3 );
			}
		}
		waitFrame();
	}

	finishAction( "pain" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 6 );
}

void monster_demon_imp::Torso_MeleeAttack() {
	playAnim( ANIMCHANNEL_TORSO, "melee_attack" );

	while( !animDone( ANIMCHANNEL_TORSO, 4 ) ) {
		waitFrame();
	}
	
	finishAction( "melee_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

void monster_demon_imp::Torso_RangeAttack() {
	disablePain();

	playAnim( ANIMCHANNEL_TORSO, range_attack_anim );
	while( !animDone( ANIMCHANNEL_TORSO, 4 ) ) {
		lookAtEnemy( 1 );	
		waitFrame();
	}

	allowMovement( true );
	finishAction( "range_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

void monster_demon_imp::Torso_TurretAttack() {
	allowMovement( false );
	disablePain();
	faceEnemy();

	playAnim( ANIMCHANNEL_TORSO, "turret_attack" );
	while( !animDone( ANIMCHANNEL_TORSO, 2 ) ) {
		waitFrame();
	}

	finishAction( "turret_attack" );
	allowMovement( true );
	animState( ANIMCHANNEL_TORSO, "Torso_TurretAttackIdle", 0 );
}

void monster_demon_imp::Torso_TurretAttackIdle() {
	faceEnemy();
	idleAnim( ANIMCHANNEL_TORSO, "turret_idle" );
	while( 1 ) {
		waitFrame();
	}
}

void monster_demon_imp::Torso_LeapAttack() {
	overrideAnim( ANIMCHANNEL_LEGS );
	disablePain();
	playAnim( ANIMCHANNEL_TORSO, "jump_start" );
	while( !animDone( ANIMCHANNEL_TORSO, 2 ) ) {
		waitFrame();
	}
	
	setBlendFrames( ANIMCHANNEL_TORSO, 2 );
	attackBegin( "melee_impLeapAttack" );
	setLinearVelocity( jumpVelocity );
	
	playCycle( ANIMCHANNEL_TORSO, "jump_loop" );
	do {
		waitFrame();
	} while( !AI_ONGROUND );
	
	attackEnd();
	playAnim( ANIMCHANNEL_TORSO, "jump_end" );
	while( !animDone( ANIMCHANNEL_TORSO, 4 ) ) {
		waitFrame();
	}

	finishAction( "leap_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

/***********************************************************************

	Legs animation control

***********************************************************************/

void monster_demon_imp::Legs_Idle() {
	idleAnim( ANIMCHANNEL_LEGS, "stand" );

	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
	}
}

void monster_demon_imp::Legs_Walk() {
	playCycle( ANIMCHANNEL_LEGS, "walk" );
	
	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
	}
}

void monster_demon_imp::Legs_Run() {
	playCycle( ANIMCHANNEL_LEGS, "run" );
	
	eachFrame {
		if ( !run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
	}
}

void monster_demon_imp::Legs_DodgeLeft() {
	playAnim( ANIMCHANNEL_LEGS, "evade_left" );

	while( !animDone( ANIMCHANNEL_LEGS, 4 ) ) {
		waitFrame();
	}
	
	finishAction( "strafe" );
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 );
}

void monster_demon_imp::Legs_DodgeRight() {
	playAnim( ANIMCHANNEL_LEGS, "evade_right" );

	while( !animDone( ANIMCHANNEL_LEGS, 4 ) ) {
		waitFrame();
	}

	finishAction( "strafe" );
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 );
}

/***********************************************************************

	AI

***********************************************************************/

/*
=====================
monster_demon_imp::init
=====================
*/
void monster_demon_imp::init() {
	run_distance	= IMP_RUNDISTANCE;
	walk_turn		= IMP_WALKTURN;
	
	setState( "state_Begin" );
}

/***********************************************************************

	States

***********************************************************************/

/*
=====================
monster_demon_imp::state_Begin
=====================
*/
void monster_demon_imp::state_Begin() {
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 0 );
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 0 );	
	monster_begin();
	setMoveType( MOVETYPE_ANIM );
	setState( "state_Idle" );
}

/*
=====================
monster_demon_imp::state_Idle
=====================
*/
void monster_demon_imp::state_Idle() {
	wait_for_enemy();

	nextLeap		= RandomTime( IMP_LEAP_RATE );
	nextAttack		= 0;
	nextDodge		= RandomTime( IMP_DODGE_RATE );
	nextNoFOVAttack = 0;
	
	setState( "state_Combat" );
}

/***********************************************************************

	attacks

***********************************************************************/

/*
=====================
monster_demon_imp::do_attack
=====================
*/
void monster_demon_imp::do_attack( float attack_flags ) {
	nextNoFOVAttack = sys.getTime() + IMP_NOFOVTIME;
	if ( attack_flags & ATTACK_DODGE_LEFT ) {
		combat_dodge_left();
	} else if ( attack_flags & ATTACK_DODGE_RIGHT ) {
		combat_dodge_right();
	} else if ( attack_flags & ATTACK_COMBAT_NODE ) {
		combat_ainode( combat_node );
	} else if ( attack_flags & ATTACK_MELEE ) {
		combat_melee();
	} else if ( attack_flags & ATTACK_LEAP ) {
		combat_leap();
	} else if ( attack_flags & ATTACK_MISSILE ) {
		combat_range();
	}
}

/*
=====================
monster_demon_imp::check_attacks
=====================
*/
float monster_demon_imp::check_attacks() {
	float range;
	float currentTime;
	float canMelee;
	float attack_flags;
	float checkLeap;
	vector vel;
	float t;
	string anim;
	vector jumpTarget;
	
	attack_flags = 0;
	
	canMelee = testMeleeAttack();
	currentTime = sys.getTime();
	if ( !canMelee ) {
		if ( AI_PAIN && ( currentTime >= nextDodge ) ) {
			if ( testAnimMove( "evade_left" ) ) {
				attack_flags |= ATTACK_DODGE_LEFT;
			}
			if ( testAnimMove( "evade_right" ) ) {
				attack_flags |= ATTACK_DODGE_RIGHT;
				
				// if we can dodge either direction, pick one
				if ( attack_flags & ATTACK_DODGE_LEFT ) {
					if ( sys.random( 100 ) < 50 ) {
						attack_flags &= ~ATTACK_DODGE_RIGHT;
					} else {
						attack_flags &= ~ATTACK_DODGE_LEFT;
					}
				}
			}
		}
	
		combat_node = getCombatNode();
		if ( combat_node ) {
			attack_flags |= ATTACK_COMBAT_NODE;
		}
	}

	if ( canMelee ) {
		attack_flags |= ATTACK_MELEE;
	}

	if ( ( ( sys.getTime() > nextNoFOVAttack ) && AI_ENEMY_VISIBLE ) || AI_ENEMY_IN_FOV ) {
		range = enemyRange();
		if ( ( range >= IMP_LEAP_RANGE_MIN ) && ( range < IMP_LEAP_RANGE_MAX ) && ( currentTime >= nextLeap ) ) {
			if ( canHitEnemy() ) {
				t = animLength( ANIMCHANNEL_TORSO, "jump_start" );
				jumpTarget = predictEnemyPos( t );
				jumpVelocity = getJumpVelocity( jumpTarget, IMP_LEAP_SPEED, IMP_LEAP_MAXHEIGHT );
				if ( jumpVelocity != '0 0 0' ) {
					attack_flags |= ATTACK_LEAP;
				} else {
					// check if we can leap again in 2 seconds
					nextLeap = DelayTime( 2 );
				}
			}
		}
		
		if ( !canReachEnemy() || ( currentTime >= nextAttack ) ) {
			range_attack_anim = chooseAnim( ANIMCHANNEL_LEGS, "turret_attack" );
			if ( canHitEnemyFromAnim( range_attack_anim ) ) {
				attack_flags |= ATTACK_MISSILE;
			}

			anim = chooseAnim( ANIMCHANNEL_LEGS, "range_attack" );
			if ( testAnimMoveTowardEnemy( anim ) ) {
				if ( canHitEnemyFromAnim( anim ) ) {
					range_attack_anim = anim;
					attack_flags |= ATTACK_MISSILE;
				}
			}
		}
	}
	
	return attack_flags;
}

/*
=====================
monster_demon_imp::combat_range
=====================
*/
void monster_demon_imp::combat_range() {
	faceEnemy();
	animState( ANIMCHANNEL_TORSO, "Torso_RangeAttack", 4 );
	waitAction( "range_attack" );

	// don't attack for a bit
	nextAttack = DelayTime( IMP_ATTACK_RATE );
	nextNoFOVAttack = sys.getTime() + IMP_NOFOVTIME;
}

/*
=====================
monster_demon_imp::combat_leap
=====================
*/
void monster_demon_imp::combat_leap() {
	stopMove();
	turnToPos( getOrigin() + jumpVelocity );
	animState( ANIMCHANNEL_TORSO, "Torso_LeapAttack", 4 );	
	waitAction( "leap_attack" );
	nextLeap = DelayTime( IMP_LEAP_RATE );
}

/*
=====================
monster_demon_imp::combat_melee
=====================
*/
void monster_demon_imp::combat_melee() {
	lookAtEnemy( 100 );
	faceEnemy();
	animState( ANIMCHANNEL_TORSO, "Torso_MeleeAttack", 5 );
	waitAction( "melee_attack" );
	lookAtEnemy( 1 );
	stopMove();
}

/*
=====================
monster_demon_imp::combat_dodge_left
=====================
*/
void monster_demon_imp::combat_dodge_left() {
	faceEnemy();
	stopMove();
	animState( ANIMCHANNEL_LEGS, "Legs_DodgeLeft", 2 );
	waitAction( "strafe" );
	nextDodge = DelayTime( IMP_DODGE_RATE );
}

/*
=====================
monster_demon_imp::combat_dodge_right
=====================
*/
void monster_demon_imp::combat_dodge_right() {
	faceEnemy();
	stopMove();
	animState( ANIMCHANNEL_LEGS, "Legs_DodgeRight", 2 );	
	waitAction( "strafe" );
	nextDodge = DelayTime( IMP_DODGE_RATE );
}

/*
=====================
monster_demon_imp::path_jump
=====================
*/
void monster_demon_imp::path_jump() {
	entity target;
	vector dir;
	vector jumpTarget;

	// walk to the path entity first
	path_corner();

	if ( checkForEnemy( true ) ) {
		return;
	}

	target = current_path.randomPath();
	if ( !target ) {
		sys.error( "missing target for '" + current_path.getName() + "'" );
	}
	jumpTarget = target.getOrigin();
	if ( !current_path.getKey( "up" ) ) {
		jumpVelocity = getJumpVelocity( jumpTarget, IMP_LEAP_SPEED, 1024 );
		if ( jumpVelocity == '0 0 0' ) {
			sys.error( "Monster '" + getName() + "' couldn't make jump from '" + current_path.getName() + "' to '" + target.getName() + "'" );
		}
	} else {
		float forward = current_path.getFloatKey( "forward" );
		if ( forward <= 0 ) {
			sys.error( "Invalid forward velocity on path_jump entity '" + current_path.getName() + "'\n" );
		}
		dir = jumpTarget - getOrigin();
		dir_z = 0;
		dir = sys.vecNormalize( dir );
		dir = dir * forward;
		dir_z = current_path.getFloatKey( "up" );
		jumpVelocity = dir;
	}
	
	stopMove();
	turnToPos( jumpTarget );
	while( !facingIdeal() ) {
		if ( checkForEnemy( true ) ) {
			return;
		}
		waitFrame();
	}

	animState( ANIMCHANNEL_TORSO, "Torso_LeapAttack", 4 );	
	waitAction( "leap_attack" );

	stopMove();
}
